buildscript {
    repositories {
        mavenLocal()
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url 'https://plugins.gradle.org/m2' }
        mavenCentral()
    }

    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
        classpath 'net.researchgate:gradle-release:2.3.4'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.12.1"
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.4'
    }
}

allprojects {

    repositories {
        mavenLocal();
        mavenCentral()
    }

    group = 'com.weghst.setaria'

    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'idea'

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    apply plugin: 'net.researchgate.release'
    apply plugin: "com.github.hierynomus.license"

    ext.slf4jVersion = '1.7.12'
    ext.testngVersion = '6.9.6'
    ext.springVersion = '4.2.5.RELEASE'

    release {
        buildTasks = ['build']

        scmAdapters = [
                net.researchgate.release.GitAdapter
        ]

        git {
            requireBranch = 'master'
            pushToRemote = 'origin'
            pushToBranchPrefix = ''
        }
    }

    license {
        header = rootProject.file('license-header.txt')
        // skipExistingHeaders = true
        ignoreFailures = true
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.name = 'The Weghst Inc.'
        ext.email = 'kevinz@weghst.com'

        excludes([
                'gradlew*', '**/gradle.properties', '**/gradle-wrapper.properties', '*.adoc',
                '*.json', '**/asserts/**', '**/js/**'
        ])
    }
}

subprojects {

    apply plugin: 'com.bmuschko.nexus'

    archivesBaseName = "$rootProject.name-$project.name"

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    modifyPom {
        project {
            url 'https://github.com/weghst/setaria'
            inceptionYear '2016'

            scm {
                connection 'scm:git:https://github.com/weghst/setaria.git'
                developerConnection 'scm:git:git@github.com:weghst/setaria.git'
                url 'https://github.com/weghst/setaria'
            }

            licenses {
                license {
                    name 'The Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }

            developers {
                developer {
                    id 'kevin'
                    name 'Kevin Zou'
                    email 'kevinz@weghst.com'
                }
            }
        }
    }

    extraArchive {
        sources = true
        tests = false
        javadoc = true
    }

    nexus {
        sign = true
        repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
        snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    dependencies {
        compile "org.slf4j:slf4j-api:$slf4jVersion"

        testCompile("org.testng:testng:$testngVersion") {
            exclude module: 'guice'
            exclude module: 'junit'
            exclude module: 'ant'
        }
    }
}
